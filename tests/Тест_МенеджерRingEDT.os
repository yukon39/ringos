#Использовать tempfiles
#Использовать fs
#Использовать "../pkg/edt"
#Использовать "../pkg/ring"

#Область ОписаниеПеременных

Перем МенеджерRingEDT;

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПослеЗапускаТеста() Экспорт
	
	ВременныеФайлы.Удалить();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиТестов

Процедура Dummy() Экспорт
	// Закрывает баг движка OneScript: https://github.com/EvilBeaver/OneScript/issues/1087 
КонецПроцедуры

&Тест
Процедура Тест_ПолучитьСписокРасположений() Экспорт
	
	// Дано
	МенеджерEDT = НовыйМенеджерEDT();
	
	// Когда
	СписокРасположений = МенеджерEDT.СписокРасположений();
	
	// Тогда
	Ожидаем.Что(СписокРасположений).ИмеетТип("Массив");
	Ожидаем.Что(СписокРасположений.Количество()).Больше(0);
	
КонецПроцедуры

&Тест
Процедура Тест_ПолучитьСписокПлатформ() Экспорт
	
	// Дано
	МенеджерEDT = НовыйМенеджерEDT();
	
	// Когда
	ПоддерживаемыеВерсии = МенеджерEDT.ПоддерживаемыеВерсииПлатформы();
	
	// Тогда
	Ожидаем.Что(ПоддерживаемыеВерсии).ИмеетТип("Массив");
	Ожидаем.Что(ПоддерживаемыеВерсии.Количество()).Больше(0);
	
КонецПроцедуры

&Тест
Процедура Тест_ДолженУстановитьРасположениеПоУмолчанию() Экспорт
	
	// Дано
	МенеджерEDT = НовыйМенеджерEDT();
	
	// Когда
	МенеджерEDT.УстановитьРасположениеПоУмолчанию("@none");
	
	// Тогда
	// Ожидаем, что код выполнился без исключения
	
КонецПроцедуры

&Тест
Процедура Тест_ДолженЭкспортироватьПроектИзКаталога() Экспорт
	
	// Дано
	МенеджерEDT = НовыйМенеджерEDT();
	ПараметрыЭкспорта = Новый ПараметрыЭкспортаEDT()
		.ИспользоватьКаталогФайловКонфигурации(ВременныеФайлы.СоздатьКаталог())
		.ИспользоватьКаталогРабочейОбласти(ВременныеФайлы.НовоеИмяФайла())
		.ИспользоватьКаталогПроекта(КаталогПроекта());
	
	// Когда
	МенеджерEDT.ЭкспортироватьПроект(ПараметрыЭкспорта);
	
	// Тогда
	ФайлКонфигурации = ОбъединитьПути(ПараметрыЭкспорта.КаталогФайловКонфигурации(), "Configuration.xml");
	Ожидаем.Что(ФС.ФайлСуществует(ФайлКонфигурации)).ЭтоИстина();
	
КонецПроцедуры

&Тест
Процедура Тест_ДолженИмпортироватьПроектИзКаталога() Экспорт
	
	// Дано
	МенеджерEDT = НовыйМенеджерEDT();
	ПараметрыИмпорта = Новый ПараметрыИмпортаEDT()
		.ИспользоватьКаталогФайловКонфигурации(КаталогФайловКонфигурации())
		.ИспользоватьКаталогРабочейОбласти(ВременныеФайлы.НовоеИмяФайла())
		.ИспользоватьКаталогПроекта(ВременныеФайлы.СоздатьКаталог());
		
	// Когда
	МенеджерEDT.ИмпортироватьПроект(ПараметрыИмпорта);
	
	// Тогда
	ФайлКонфигурации = ОбъединитьПути(ПараметрыИмпорта.КаталогПроекта(), "src", "Configuration", "Configuration.mdo");
	Ожидаем.Что(ФС.ФайлСуществует(ФайлКонфигурации)).ЭтоИстина();
	
КонецПроцедуры

&Тест
Процедура Тест_ДолженПроверитьПроект() Экспорт
	
	// Дано
	МенеджерEDT = НовыйМенеджерEDT();
	ПараметрыПроверки = Новый ПараметрыПроверкиEDT()
		.ИспользоватьКаталогРабочейОбласти(ВременныеФайлы.НовоеИмяФайла())
		.ИспользоватьФайлРезультата(ВременныеФайлы.НовоеИмяФайла())
		.ДобавитьКаталогПроекта(КаталогПроекта());
	
	// Когда
	МенеджерEDT.ПроверитьПроект(ПараметрыПроверки);
	
	// Тогда
	Ожидаем.Что(ФС.ФайлСуществует(ПараметрыПроверки.ФайлРезультата())).ЭтоИстина();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйМенеджерEDT()
	
	Если МенеджерRingEDT = Неопределено Тогда
		
		МенеджерRing = Новый МенеджерRing();
		СписокМодулей = МенеджерRing.УстановленныеМодули();
		МодульEDT = СписокМодулей.НайтиМодули("edt")[0];
		
		МенеджерRingEDT = Новый МенеджерRingEDT(МодульEDT);
		
	КонецЕсли;
	
	Возврат МенеджерRingEDT;
	
КонецФункции

Функция КаталогПроекта()
	Возврат ОбъединитьПути("fixtures", "src", "edt");
КонецФункции

Функция КаталогФайловКонфигурации()
	Возврат ОбъединитьПути("fixtures", "src", "xml");
КонецФункции

#КонецОбласти